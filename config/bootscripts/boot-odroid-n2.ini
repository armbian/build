ODROIDN2-UBOOT-CONFIG

setenv rootdev "/dev/mmcblk0p1"
setenv rootfstype "ext4"

ODROIDN2-UBOOT-CONFIG

# Display splash logo
setenv bootlogo "false"

# legacy and mainline kernel diff
if ext4load mmc ${devno}:1 0x00000000 "/boot/.next" || fatload mmc ${devno}:1 0x00000000 ".next"  || ext4load mmc ${devno}:1 0x00000000 ".next"; then 
	echo "Found mainline kernel configuration"
	setenv uartconsole "ttyAML0,115200n8"
	setenv kernelimage "uImage"
else
	echo "Found legacy kernel configuration"
	setenv uartconsole "ttyS0,115200n8"
	setenv kernelimage "zImage"
fi

# Default Console Device Setting
setenv condev "console=${uartconsole} console=tty1 loglevel=1"   # on both

# Auto Detection of Monitor settings based on your Screen information
setenv display_autodetect "true"

# HDMI Mode
# Resolution Configuration
#    Symbol             | Resolution
# ----------------------+-------------
#    "480x320p60hz"     | 480x320 Progressive 60Hz
#    "480p60hz"         | 720x480 Progressive 60Hz
#    "576p50hz"         | 720x576 Progressive 50Hz
#    "720p60hz"         | 1280x720 Progressive 60Hz
#    "720p50hz"         | 1280x720 Progressive 50Hz
#    "1080p60hz"        | 1920x1080 Progressive 60Hz
#    "1080p50hz"        | 1920x1080 Progressive 50Hz
#    "1080p30hz"        | 1920x1080 Progressive 30Hz
#    "1080p24hz"        | 1920x1080 Progressive 24Hz
#    "1080i60hz"        | 1920x1080 Interlaced 60Hz
#    "1080i50hz"        | 1920x1080 Interlaced 50Hz
#    "2160p60hz"        | 3840x2160 Progressive 60Hz
#    "2160p50hz"        | 3840x2160 Progressive 50Hz
#    "2160p30hz"        | 3840x2160 Progressive 30Hz
#    "2160p25hz"        | 3840x2160 Progressive 25Hz
#    "2160p24hz"        | 3840x2160 Progressive 24Hz
#    "smpte24hz"        | 3840x2160 Progressive 24Hz SMPTE
#    "2160p60hz420"     | 3840x2160 Progressive 60Hz YCbCr 4:2:0
#    "2160p50hz420"     | 3840x2160 Progressive 50Hz YCbCr 4:2:0
#    "640x480p60hz"     | 640x480 Progressive 60Hz
#    "800x480p60hz"     | 800x480 Progressive 60Hz
#    "800x600p60hz"     | 800x600 Progressive 60Hz
#    "1024x600p60hz"    | 1024x600 Progressive 60Hz
#    "1024x768p60hz"    | 1024x768 Progressive 60Hz
#    "1280x800p60hz"    | 1280x800 Progressive 60Hz
#    "1280x1024p60hz"   | 1280x1024 Progressive 60Hz
#    "1360x768p60hz"    | 1360x768 Progressive 60Hz
#    "1440x900p60hz"    | 1440x900 Progressive 60Hz
#    "1600x900p60hz"    | 1600x900 Progressive 60Hz
#    "1600x1200p60hz"   | 1600x1200 Progressive 60Hz
#    "1680x1050p60hz"   | 1680x1050 Progressive 60Hz
#    "1920x1200p60hz"   | 1920x1200 Progressive 60Hz
#    "2560x1080p60hz"   | 2560x1080 Progressive 60Hz
#    "2560x1440p60hz"   | 2560x1440 Progressive 60Hz
#    "2560x1600p60hz"   | 2560x1600 Progressive 60Hz
#    "3440x1440p60hz"   | 3440x1440 Progressive 60Hz
setenv hdmimode "1080p60hz"

# Overscan percentage
# This value scales down the actual screen size by the percentage below
# valid range is 80 to 100
setenv overscan "100"

### voutmode : hdmi or dvi
setenv voutmode "hdmi"
# setenv voutmode "dvi"

# HPD enable/disable option
setenv disablehpd "false"

# max cpu frequency in MHz unit
if test "${variant}" = "n2_plus"; then
	setenv fdtfile meson-g12b-odroid-n2-plus.dtb
	setenv max_freq_a73 "2208"
	setenv max_freq_a53 "1908"
else
	setenv fdtfile meson-g12b-odroid-n2.dtb
	setenv max_freq_a73 "1800"
	setenv max_freq_a53 "1896"
fi
# A73
# N2: 500/667/1000/1200/1398/1512/1608/1704/1800 (Stock)/1908/2004"
# N2 Plus: 500/667/1000/1200/1398/1512/1608/1704/1800/1908/2016/2100/2208 (Stock)/2304/2400

# A53
# N2: 100/250/500/667/1000/1200/1398/1512/1608/1704/1896 (Stock)/1992
# N2 Plus: 500/667/1000/1200/1398/1512/1608/1704/1800/1908 (Stock)/2016

# max cpu-cores
# Note:
# CPU's 0 and 1 are the A53 (small cores)
# CPU's 2 to 5 are the A73 (big cores)
# Lowering this value disables only the bigger cores (the last cores).
# setenv maxcpus "4"
# setenv maxcpus "5"
setenv maxcpus "6"

### Normal HDMI Monitors
if test "${display_autodetect}" = "true"; then hdmitx edid; fi
if test "${bootlogo}" = "true"; then setenv bootsplash "bootsplash.bootfile=bootsplash.armbian"; fi
if test "${hdmimode}" = "custombuilt"; then setenv cmode "modeline=${modeline}"; fi

# Read armbianEnv.txt
if ext4load mmc ${devno}:1 0x44000000 /boot/armbianEnv.txt || fatload mmc ${devno}:1 0x44000000 armbianEnv.txt || ext4load mmc ${devno}:1 0x44000000 armbianEnv.txt; then env import -t 0x44000000 ${filesize}; fi

# Boot Args
setenv bootargs "root=${rootdev} rootwait rootfstype=${rootfstype} ${condev} ${amlogic} no_console_suspend fsck.repair=yes net.ifnames=0 elevator=noop hdmimode=${hdmimode} cvbsmode=576cvbs max_freq_a53=${max_freq_a53} max_freq_a73=${max_freq_a73} maxcpus=${maxcpus} voutmode=${voutmode} ${cmode} disablehpd=${disablehpd} ${bootsplash} cvbscable=${cvbscable} overscan=${overscan} consoleblank=0"

# Set load addresses
setenv dtb_loadaddr "0x1000000"
setenv k_addr "0x1100000"
setenv loadaddr "0x1B00000"
setenv initrd_loadaddr "0x3700000"

# Load kernel, dtb and initrd
ext4load mmc ${devno}:1 ${k_addr} ${kernelimage} || ext4load mmc ${devno}:1 ${k_addr} boot/${kernelimage}
ext4load mmc ${devno}:1 ${dtb_loadaddr} dtb/amlogic/${fdtfile} || ext4load mmc ${devno}:1 ${dtb_loadaddr} boot/dtb/amlogic/${fdtfile}
ext4load mmc ${devno}:1 ${initrd_loadaddr} uInitrd || ext4load mmc ${devno}:1 ${initrd_loadaddr} boot/uInitrd

if test "${kernelimage}" = "zImage"; then
	fdt addr ${dtb_loadaddr}
	# unzip the kernel
	unzip ${k_addr} ${loadaddr}
	# boot legacy kernel
	booti ${loadaddr} ${initrd_loadaddr} ${dtb_loadaddr}
else
	# boot mainline kernel
	bootm ${k_addr} ${initrd_loadaddr} ${dtb_loadaddr}
fi
